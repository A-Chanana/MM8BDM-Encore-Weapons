actor JusticeMeteorWep_NormalBar : NormalBar{args 192,195}
actor JusticeMeteorWep : BaseMM8BDMWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_JUSTICEMETEOR"
Obituary "$OB_JUSTICEMETEOR"
Tag "$TAG_JUSTICEMETEOR"
weapon.ammotype "JusticeMeteorAmmo"
inventory.icon "JMETICON"
States
{
SpawnLoop:
JMET A -1
loop

Ready:
JMET B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_JUSTICEMETEOR)
JMET BB 1 A_WeaponReady
goto Ready+1

ReadyC:
Goto Ready+1
Deselect:
JMET B 0 A_SetTranslucent(1.0,0)
Goto DeselectSwap
Select:
JMET B 0 
Goto SelectSwap

Fire:
JMET B 0 A_JumpIfNoAmmo("Fist")
JMET B 0 A_GiveInventory("JusticeMeteorProtect",1)
JMET B 0 ACS_NamedExecuteWithResult("core_flyplayer",0,2)
JMET B 0 ACS_NamedExecuteWithResult("core_stopplayer",0,1)
JMET B 0 A_PlaySoundEx("weapon/puritymeteorcharge","Weapon")
JMET D 3 A_SpawnItemEx("JusticeMeteorSparkSpawner")
JMET E 4 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET F 3 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET G 2 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET H 1
JMET I 3
JMET M 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JUSTICEMETEOR,TCLR_NONE)
JMET M 0 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET I 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JMETEORCHRG,TCLR_CHARGE1)
JMET M 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JUSTICEMETEOR,TCLR_NONE)
JMET M 0 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET I 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JMETEORCHRG,TCLR_CHARGE1)
JMET M 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JUSTICEMETEOR,TCLR_NONE)
JMET M 0 ACS_NamedExecuteAlways("Enc_StateChange",0,2)
JMET I 3 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JMETEORCHRG,TCLR_CHARGE1)
goto DashBegin

Altfire:
JMET B 0 A_JumpIfInventory("ENC_ClassAltfire",1,"ClassAltfire")
JMET B 0 A_JumpIf(CallACS("Enc_class_altfire"),"ClassAltfire")
JMET B 1 A_JumpIfInventory("MM8BDMPlayer",1,1)
wait
JMET B 0
Fist:
JMET B 0 A_JumpIfInventory("DuoPunchCooldown",1,"NoAmmo")
JMET B 0 A_PlaySoundEx("weapons/busters/duofistfire2","Weapon")
JMET B 0 A_FireCustomMissile("MidFist",0,0,8,0)
JMET J 2 A_GiveInventory("DuoPunchCooldown",1)
JMET L 2
JMET K 2
JMET J 6
JMET B 1 A_Refire("Fist")
JMET B 0 A_JumpIfInventory("IsBot",1,"NoAmmo")
goto Ready+1

DashBegin:
JMET B 0 ACS_NamedExecuteWithResult("core_stopplayer",0,0)
JMET B 0 ACS_NamedExecuteWithResult("core_freezeplayer",0,1)
JMET B 0 A_ChangeFlag("INVULNERABLE",1)
JMET B 0 A_StopSoundEx ("SoundSlot5")
JMET B 0 A_PlaySoundEx("weapon/puritymeteorlaunch","Weapon")
JMET H 1
JMET GF 1 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JUSTICEMETEOR,TCLR_NONE)
JMET B 0 A_SetTranslucent(0.0,0)
JMET E 1 A_SpawnItemEx("JusticeMeteorFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
JMET Z 0 A_ChangeVelocity(70*cos(pitch),0,70*sin(-pitch),CVF_RELATIVE|CVF_REPLACE)
JMET Z 0 A_SetArg(0,momx)
JMET Z 0 A_SetArg(1,momy)
JMET Z 0 A_SetArg(2,momz)
DashLoop:
JMET Z 0 A_ChangeVelocity(args[0],args[1],args[2],CVF_REPLACE)
JMET Z 1 A_FireCustomMissile("JusticeMeteorCheck",0,0,0,-15)
JMET Z 0 A_FireCustomMissile("JusticeMeteorDamager",0,0,0,-20)
JMET Z 0 A_JumpIfInventory("JusticeMeteorRecoil",1,"DashStop")
JMET Z 0 A_TakeInventory("JusticeMeteorAmmo",1)
JMET Z 0 A_Refire("DashCancel")
JMET Z 0 A_JumpIfNoAmmo("DashCancel")
loop

DashStop:
JMET B 0 A_PlaySoundEx("misc/hugequake2","Body")
JMET B 0 A_PlaySoundEx("weapon/puritymeteorimpact","weapon")
JMET B 0 A_TakeInventory("JusticeMeteorAmmo",4,TIF_NOTAKEINFINITE)
JMET B 0 ACS_NamedExecuteWithResult("core_chargecolor",CLR_JUSTICEMETEOR,TCLR_NONE)
JMET B 0 A_SetTranslucent(1.0,0)
JMET B 0 A_ChangeFlag("INVULNERABLE",0)
JMET C 1 A_SpawnItemEx("JusticeMeteorExplosion",16,0,0)
JMET B 0 ACS_NamedExecuteWithResult("core_flyplayer",0,0)
JMET A 0 ACS_NamedExecuteWithResult("core_freezeplayer",0,0)
JMET B 0 A_ZoomFactor(1.0)
JMET G 0 A_TakeInventory("JusticeMeteorAmmo",4)
JMET B 0 A_GiveInventory("JusticeMeteorRecoil",1)
JMET CB 4
JMET B 10
goto Ready+1

DashCancel:
JMET B 0 A_PlaySoundEx("weapon/puritymeteorcancel","weapon")
goto DashStop+2

NoAmmo:
JMET B 1 ACS_NamedExecute("core_noammo",0)
goto Ready+1

ClassAltfire:
JMET B 0 
goto Super::Altfire
}
}

actor JusticeMeteorRecoil : Powerup {powerup.duration 10}

actor JusticeMeteorAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor JusticeMeteorFX
{
PROJECTILE
+NOCLIP
+NOINTERACTION
+THRUGHOST
height 32
radius 32
speed 50
scale 2.5
States
{
Spawn:
JMET QP 1 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION )
JMET Q 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"Death")
JMET Q 0 A_JumpIfInTargetInventory("JusticeMeteorRecoil",1,"Death")
JMET P 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
JMET Q 0 A_SpawnItemEx("JusticeMeteorSparkFX",16,0,16)
loop
Death:
JMET Q 0
stop
}
}

actor JusticeMeteorDamager
{
PROJECTILE
+RIPPER
Obituary "$OB_JUSTICEMETEOR"
Damagetype "JusticeMeteor"
height 32
radius 32
speed 35
Damage (CallACS("enc_cbm_damage",35))
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 1 //A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION )
stop
}
}

actor JusticeMeteorCheck : FastProjectile
{
PROJECTILE
Obituary "$OB_JUSTICEMETEOR"
Damagetype "JusticeMeteor"
height 32
radius 18
speed 50
Damage (0)
States
{
Spawn:
TNT1 A 0
TNT1 A 1
stop
Death:
TNT1 A 0 A_SpawnItemEx("JusticeMeteorQuake")
TNT1 A 0 A_GiveToTarget("JusticeMeteorRecoil",1)
stop
}
}

actor JusticeMeteorQuake
{
+NOINTERACTION
+NOCLIP
States
{
Spawn:
TNT1 A 0
TNT1 A 0 Radius_Quake(2,4,0,2,0)
TNT1 A 1
stop
}
}

actor JusticeMeteorProtect : PowerProtection
{
damagefactor "Normal",0.26
powerup.duration -5
}

actor DuoPunchCooldown : Powerup
{
powerup.duration -1
}

actor JusticeMeteorSparkFX : BasicGraphicEffect
{
translation "198:198=195:195","4:4=192:192"
PROJECTILE
+NOCLIP
+NOINTERACTION
+THRUGHOST
+CLIENTSIDEONLY
height 32
radius 32
speed 10
Damage (0)
scale 2.5
States
{
Spawn:
INTQ AA 0 A_GiveInventory("ProjTeamColor1Client",1)
INTQ ABCDEFGFEDCBA 1
stop
}
}

actor JusticeMeteorSparkSpawner
{
PROJECTILE
+NOCLIP
+NOINTERACTION
+THRUGHOST
+CLIENTSIDEONLY
Obituary "$OB_JUSTICEMETEOR"
Damagetype "JusticeMeteor"
height 32
radius 32
speed 10
Damage (0)
scale 5.0
translation "198:198=195:195","4:4=192:192"
reactiontime 49
States
{
Spawn:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION)
TNT1 A 1 A_SpawnItemEX("JusticeMeteorSparkFX",0,0,-12,4,0,4,random(0,360))
TNT1 A 0 A_Countdown
loop
}
}

actor JusticeMeteorExplosion : PharaohShot3
{
PROJECTILE
Obituary "$OB_JUSTICEMETEOR"
Damagetype "JusticeMeteor"
+NOCLIP
damage (0)
radius 16
height 16
Scale 5.0
States
{
Spawn:
TNT1 E 0
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
TNT1 A 0 A_Explode(CallACS("enc_cbm_damage",45),128,0,0,16)
JMET LMNO 2
stop
}
}